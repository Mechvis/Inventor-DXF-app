<html>
  <head>
    <META http-equiv="Content-Type" content="text/html; charset=utf-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge">
    <meta name="topic-type" content="reference-adsk">
    <meta name="helpsystempath" content="/view/INVNTOR/2026/ENU/">
    <link rel="canonical" href="/cloudhelp/2026/ENU/Inventor-API/files/iFeatureTable_Sample.htm#!">
    <meta name="product" content="INVNTOR">
    <meta name="release" content="2026">
    <meta name="book" content="Developer">
    <meta name="component" content="Inventor-API">
    <meta name="created" content="">
    <meta name="user-comments" content="enable">
    <meta name="contextid" content="iFeatureTable_Sample">
    <meta name="description" content="Place table driven iFeature Sample">
    <meta name="topicid" content="iFeatureTable_Sample">
    <meta name="experiencelevel" content="expert">
    <meta name="audience" content="programmer">
    <meta name="language" content="ENU">
    <title>Place table driven iFeature</title>
    <link rel="stylesheet" href="../Style/jquery-ui.css"><script src="../Scripts/jquery-1.11.1.js"></script><script src="../Scripts/jquery-ui.js"></script><link rel="stylesheet" type="text/css" href="../Style/APIHelp-chm.css">
    <link rel="stylesheet" type="text/css" href="/view/clientframework/client.css"><script type="text/javascript" src="/view/clientframework/client.js"></script><script>
          // This turns on the display of the copyright notice which is not visible by default.
          // Because the head is removed when used in the web help, this function will only exists
          // when the chm help is being created.
          window.onload = function() {
          copyrightElement = document.getElementById('CopyrightNotice');
          copyrightElement.style.display = 'block';
          };
        </script><script>
          $(function() {
          $( "#tabs" ).tabs();
          });
        </script></head>
  <body><script>
          function copyCode(language) {
          // Get the pre element for the specified language.
          var preElement = document.getElementById(language);

          // Get the text from the element and copy it to the clipboard.
          var preText = preElement.innerText;

          var isIE = /*@cc_on!@*/false || !!document.documentMode;

          if (isIE)
          {
            window.clipboardData.setData('Text', preText);
          }
          else
          {
            navigator.clipboard.writeText(preText);
          }

          //alert("Code copied to clipboard");
          }
        </script><link rel="stylesheet" type="text/css" href="../Style/APIHelp.css">
    <h1 class="api">Place table driven iFeature</h1>
    <h2 class="api">
          Description
        </h2>This program demonstrates the placement of a table driven iFeature in a part.<h2 class="api">
          Code Samples
        </h2>
    <div id="tabs">
      <ul>
        <li><a href="#VBA">VBA</a></li>
        <li><a href="#iLogic">iLogic</a></li>
      </ul>
      <div id="VBA" class="api-code">A part must be open and a planar face on that part selected. There aren't any table driven .ide files delivered with Inventor so one must be constructed to be used as input. The primary assumption this sample makes about the .ide file is that a custom row has been created named 'Size' and one of the rows in this column has the value '4.5.'<table width="100%" style="font-size:15">
          <tr>
            <td align="right"><button onclick="copyCode('VBA_code')" class="copy-code">Copy Code</button></td>
          </tr>
        </table>
        <pre class="api-code" id="VBA_code">Public Sub PlaceTableDriveniFeature()
    <span style="color:blue">' Get the part document and component definition of the active document.</span>
    On Error Resume Next
    Dim oPartDoc As PartDocument
    Set oPartDoc = ThisApplication.ActiveDocument
    If Err Then
        MsgBox "A part must be active."
        Exit Sub
    End If

    Dim oPartDef As PartComponentDefinition
    Set oPartDef = oPartDoc.ComponentDefinition

    <span style="color:blue">' Get the selected face to use as input for the iFeature.</span>
    Dim oFace As Face
    Set oFace = oPartDoc.SelectSet.Item(1)
    If Err Then
        MsgBox "A planar face must be selected."
        Exit Sub
    End If
    On Error GoTo 0

    If oFace.SurfaceType &lt;&gt; kPlaneSurface Then
        MsgBox "A planar face must be selected."
        Exit Sub
    End If

    Dim oFeatures As PartFeatures
    Set oFeatures = oPartDef.Features

    <span style="color:blue">' Create an iFeatureDefinition object.</span>
    Dim oiFeatureDef As iFeatureDefinition
    Set oiFeatureDef = oFeatures.iFeatures.CreateiFeatureDefinition( _
"C:\Users\Public\Documents\Autodesk\Inventor 2026\Catalog\Table\Test.ide")

    <span style="color:blue">' Set the input, which in this case is only the sketch plane</span>
    <span style="color:blue">' since the other input comes from the table.  The parameter input</span>
    <span style="color:blue">' should not be available at this point since it can't be changed</span>
    <span style="color:blue">' and is controlled by the table.</span>
    <span style="color:blue">'</span>
    <span style="color:blue">' When an existing table driven iFeature is accessed then this should</span>
    <span style="color:blue">' include the parameters so the programmer has access to the corresponding</span>
    <span style="color:blue">' reference parameter that's created.</span>
    Dim bFoundPlaneInput As Boolean
    bFoundPlaneInput = False
    Dim oInput As iFeatureInput
    For Each oInput In oiFeatureDef.iFeatureInputs
        Dim oParamInput As iFeatureParameterInput
        Select Case oInput.Name
            Case "Profile Plane1"
                Dim oPlaneInput As iFeatureSketchPlaneInput
                Set oPlaneInput = oInput
                oPlaneInput.PlaneInput = oFace
                bFoundPlaneInput = True
        End Select
    Next

    If Not bFoundPlaneInput Then
        MsgBox "The ide file does not contain an iFeature input named ""Profile Plane1""."
        Exit Sub
    End If

    <span style="color:blue">'** Look through the table to find the row where "Size" is "4.5".</span>
    Dim oTable As iFeatureTable
    Set oTable = oiFeatureDef.iFeatureTable

    <span style="color:blue">' Find the "Size" column.</span>
    Dim oColumn As iFeatureTableColumn
    Dim bFoundSize As Boolean
    bFoundSize = False
    For Each oColumn In oTable.iFeatureTableColumns
        If oColumn.DisplayHeading = "Size" Then
            bFoundSize = True
            Exit For
        End If
    Next

    If Not bFoundSize Then
        MsgBox "The column ""Size"" was not found."
        Exit Sub
    End If

    <span style="color:blue">' Find the row in the "Size" column with the value of "4.5"</span>
    Dim oCell As iFeatureTableCell
    bFoundSize = False
    For Each oCell In oColumn
        If oCell.Value = "4.5" Then
            bFoundSize = True
            Exit For
        End If
    Next

    If Not bFoundSize Then
        MsgBox "The cell with value ""4.5"" was not found."
        Exit Sub
    End If

    <span style="color:blue">' Set this row as the active row.</span>
    oiFeatureDef.ActiveTableRow = oCell.Row

    <span style="color:blue">' Create the iFeature.</span>
    Dim oiFeature As iFeature
    Set oiFeature = oFeatures.iFeatures.Add(oiFeatureDef)
End Sub</pre>
      </div>
      <div id="iLogic" class="api-code">A part must be open and a planar face on that part selected. There aren't any table driven .ide files delivered with Inventor so one must be constructed to be used as input. The primary assumption this sample makes about the .ide file is that a custom row has been created named 'Size' and one of the rows in this column has the value '4.5.'<table width="100%" style="font-size:15">
          <tr>
            <td align="right"><button onclick="copyCode('iLogic_code')" class="copy-code">Copy Code</button></td>
          </tr>
        </table>
        <pre class="api-code" id="iLogic_code">    <span style="color:blue">' Get the part document and component definition of the active document.</span>
    On Error Resume Next
    Dim oPartDoc As PartDocument
    oPartDoc = ThisApplication.ActiveDocument
    If Err.Number &gt; 0 Then
        MsgBox("A part must be active.")
        Exit Sub
    End If

    Dim oPartDef As PartComponentDefinition
    oPartDef = oPartDoc.ComponentDefinition

    <span style="color:blue">' Get the selected face to use as input for the iFeature.</span>
    Dim oFace As Face
    oFace = oPartDoc.SelectSet.Item(1)
    If Err.Number &gt; 0 Then
        MsgBox("A planar face must be selected.")
        Exit Sub
    End If
    On Error GoTo 0

    If oFace.SurfaceType &lt;&gt; kPlaneSurface Then
        MsgBox("A planar face must be selected.")
        Exit Sub
    End If

    Dim oFeatures As PartFeatures
    oFeatures = oPartDef.Features

    <span style="color:blue">' Create an iFeatureDefinition object.</span>
    Dim oiFeatureDef As iFeatureDefinition
    oiFeatureDef = oFeatures.iFeatures.CreateiFeatureDefinition( _
"C:\Users\Public\Documents\Autodesk\Inventor 2026\Catalog\Table\Test.ide")

    <span style="color:blue">' Set the input, which in this case is only the sketch plane</span>
    <span style="color:blue">' since the other input comes from the table.  The parameter input</span>
    <span style="color:blue">' should not be available at this point since it can't be changed</span>
    <span style="color:blue">' and is controlled by the table.</span>
    <span style="color:blue">'</span>
    <span style="color:blue">' When an existing table driven iFeature is accessed then this should</span>
    <span style="color:blue">' include the parameters so the programmer has access to the corresponding</span>
    <span style="color:blue">' reference parameter that's created.</span>
    Dim bFoundPlaneInput As Boolean
    bFoundPlaneInput = False
    Dim oInput As iFeatureInput
    For Each oInput In oiFeatureDef.iFeatureInputs
        Dim oParamInput As iFeatureParameterInput
        Select Case oInput.Name
            Case "Profile Plane1"
                Dim oPlaneInput As iFeatureSketchPlaneInput
                oPlaneInput = oInput
                oPlaneInput.PlaneInput = oFace
                bFoundPlaneInput = True
        End Select
    Next

    If Not bFoundPlaneInput Then
        MsgBox("The ide file does not contain an iFeature input named ""Profile Plane1"".")
        Exit Sub
    End If

    <span style="color:blue">'** Look through the table to find the row where "Size" is "4.5".</span>
    Dim oTable As iFeatureTable
    oTable = oiFeatureDef.iFeatureTable

    <span style="color:blue">' Find the "Size" column.</span>
    Dim oColumn As iFeatureTableColumn
    Dim bFoundSize As Boolean
    bFoundSize = False
    For Each oColumn In oTable.iFeatureTableColumns
        If oColumn.DisplayHeading = "Size" Then
            bFoundSize = True
            Exit For
        End If
    Next

    If Not bFoundSize Then
        MsgBox("The column ""Size"" was not found.")
        Exit Sub
    End If

    <span style="color:blue">' Find the row in the "Size" column with the value of "4.5"</span>
    Dim oCell As iFeatureTableCell
    bFoundSize = False
    For Each oCell In oColumn
        If oCell.Value = "4.5" Then
            bFoundSize = True
            Exit For
        End If
    Next

    If Not bFoundSize Then
        MsgBox("The cell with value ""4.5"" was not found.")
        Exit Sub
    End If

    <span style="color:blue">' Set this row as the active row.</span>
    oiFeatureDef.ActiveTableRow = oCell.Row

    <span style="color:blue">' Create the iFeature.</span>
    Dim oiFeature As iFeature
    oiFeature = oFeatures.iFeatures.Add(oiFeatureDef)</pre>
      </div>
    </div><br><br><div id="CopyrightNotice" style="display:none;">
      <hr>
      <table width="100%" style="font-size:15">
        <tr>
          <td align="left">
                Â© Copyright 2025 Autodesk, Inc.
              </td>
          <td align="right"><a href="mailto:mfg.api.help.comments@autodesk.com?subject=Inventor%202026%20API%20Help%20(iFeatureTable%20Sample%20Help%20Topic)">Comment on this page.</a></td>
        </tr>
      </table>
    </div>
  </body>
</html>