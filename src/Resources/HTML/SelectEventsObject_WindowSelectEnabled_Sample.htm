<html>
  <head>
    <META http-equiv="Content-Type" content="text/html; charset=utf-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge">
    <meta name="topic-type" content="reference-adsk">
    <meta name="helpsystempath" content="/view/INVNTOR/2026/ENU/">
    <link rel="canonical" href="/cloudhelp/2026/ENU/Inventor-API/files/SelectEventsObject_WindowSelectEnabled_Sample.htm#!">
    <meta name="product" content="INVNTOR">
    <meta name="release" content="2026">
    <meta name="book" content="Developer">
    <meta name="component" content="Inventor-API">
    <meta name="created" content="">
    <meta name="user-comments" content="enable">
    <meta name="contextid" content="SelectEventsObject_WindowSelectEnabled_Sample">
    <meta name="description" content="Window Selection Sample">
    <meta name="topicid" content="SelectEventsObject_WindowSelectEnabled_Sample">
    <meta name="experiencelevel" content="expert">
    <meta name="audience" content="programmer">
    <meta name="language" content="ENU">
    <title>Window Selection</title>
    <link rel="stylesheet" href="../Style/jquery-ui.css"><script src="../Scripts/jquery-1.11.1.js"></script><script src="../Scripts/jquery-ui.js"></script><link rel="stylesheet" type="text/css" href="../Style/APIHelp-chm.css">
    <link rel="stylesheet" type="text/css" href="/view/clientframework/client.css"><script type="text/javascript" src="/view/clientframework/client.js"></script><script>
          // This turns on the display of the copyright notice which is not visible by default.
          // Because the head is removed when used in the web help, this function will only exists
          // when the chm help is being created.
          window.onload = function() {
          copyrightElement = document.getElementById('CopyrightNotice');
          copyrightElement.style.display = 'block';
          };
        </script><script>
          $(function() {
          $( "#tabs" ).tabs();
          });
        </script></head>
  <body><script>
          function copyCode(language) {
          // Get the pre element for the specified language.
          var preElement = document.getElementById(language);

          // Get the text from the element and copy it to the clipboard.
          var preText = preElement.innerText;

          var isIE = /*@cc_on!@*/false || !!document.documentMode;

          if (isIE)
          {
            window.clipboardData.setData('Text', preText);
          }
          else
          {
            navigator.clipboard.writeText(preText);
          }

          //alert("Code copied to clipboard");
          }
        </script><link rel="stylesheet" type="text/css" href="../Style/APIHelp.css">
    <h1 class="api">Window Selection</h1>
    <h2 class="api">
          Description
        </h2>This sample demonstrates using the selection events to window-select multiple edges. Selection is dependent on events and VB only supports events within a class module.<h2 class="api">
          Code Samples
        </h2>
    <div id="tabs">
      <ul>
        <li><a href="#VBA">VBA</a></li>
        <li><a href="#iLogic">iLogic</a></li>
      </ul>
      <div id="VBA" class="api-code">To use the sample copy the TestWindowSelection sub into a code module. Create a new class module called clsSelect and copy all of the rest of the code into it. To run the sample, have a part document open that contains some geometry and run the TestWindowSelection sub.<table width="100%" style="font-size:15">
          <tr>
            <td align="right"><button onclick="copyCode('VBA_code')" class="copy-code">Copy Code</button></td>
          </tr>
        </table>
        <pre class="api-code" id="VBA_code">Private oSelect As clsSelect

Public Sub TestWindowSelection()
    <span style="color:blue">' Create a new clsSelect object.</span>
    Set oSelect = New clsSelect

    <span style="color:blue">' Call the WindowSelect method of the clsSelect object</span>
    oSelect.WindowSelect
End Sub

<span style="color:blue">'*************************************************************</span>
<span style="color:blue">' The declarations and functions below need to be copied into</span>
<span style="color:blue">' a class module whose name is "clsSelect". The name can be</span>
<span style="color:blue">' changed but you'll need to change the declaration in the</span>
<span style="color:blue">' calling function "TestWindowSelection" to use the new name.</span>

<span style="color:blue">' Declare the event objects</span>
Private WithEvents oInteractEvents As InteractionEvents
Private WithEvents oSelectEvents As SelectEvents

<span style="color:blue">' Declare a flag that's used to determine if command prompts are shown as tooltips.</span>
Private bTooltipEnabled As Boolean

Public Function WindowSelect()
    <span style="color:blue">' Create an InteractionEvents object.</span>
    Set oInteractEvents = ThisApplication.CommandManager.CreateInteractionEvents

    <span style="color:blue">' Ensure interaction is enabled.</span>
    oInteractEvents.InteractionDisabled = False

    <span style="color:blue">' Set a reference to the select events.</span>
    Set oSelectEvents = oInteractEvents.SelectEvents

    <span style="color:blue">' Set the filter for circular edges (this includes circular arcs).</span>
    oSelectEvents.AddSelectionFilter kPartEdgeCircularFilter

    oSelectEvents.WindowSelectEnabled = True

    bTooltipEnabled = ThisApplication.GeneralOptions.ShowCommandPromptTooltips
    ThisApplication.GeneralOptions.ShowCommandPromptTooltips = True

    oInteractEvents.StatusBarText = "Window select. Esc to exit."

    <span style="color:blue">' Start the InteractionEvents object.</span>
    oInteractEvents.Start
End Function

Private Sub oInteractEvents_OnTerminate()
    <span style="color:blue">' Reset to original value</span>
    ThisApplication.GeneralOptions.ShowCommandPromptTooltips = bTooltipEnabled

    <span style="color:blue">' Clean up.</span>
    Set oSelectEvents = Nothing
    Set oInteractEvents = Nothing
End Sub

Private Sub oSelectEvents_OnPreSelect(PreSelectEntity As Object, DoHighlight As Boolean, MorePreSelectEntities As ObjectCollection, ByVal SelectionDevice As SelectionDeviceEnum, ByVal ModelPosition As Point, ByVal ViewPosition As Point2d, ByVal View As View)
    <span style="color:blue">' Set a reference to the selected edge.</span>
    <span style="color:blue">' Only circular edges can come through since the circular edge filter was set.</span>
    Dim oEdge As Edge
    Set oEdge = PreSelectEntity

    <span style="color:blue">' Allow only fully circular edges to be picked.</span>
    If Not oEdge.GeometryType = kCircleCurve Then
      DoHighlight = False
    End If
End Sub

Private Sub oSelectEvents_OnSelect(ByVal JustSelectedEntities As ObjectsEnumerator, ByVal SelectionDevice As SelectionDeviceEnum, ByVal ModelPosition As Point, ByVal ViewPosition As Point2d, ByVal View As View)
    MsgBox "Picked " & JustSelectedEntities.Count & " circular edges."
End Sub</pre>
      </div>
      <div id="iLogic" class="api-code">To run the sample, have a part document open that contains some geometry and run the sample.<table width="100%" style="font-size:15">
          <tr>
            <td align="right"><button onclick="copyCode('iLogic_code')" class="copy-code">Copy Code</button></td>
          </tr>
        </table>
        <pre class="api-code" id="iLogic_code">Class Test
	Private oSelect As clsSelect
	
	Sub Main
	    <span style="color:blue">' Create a new clsSelect object.</span>
	    oSelect = New clsSelect
		oSelect.ThisApplication = ThisApplication
	    <span style="color:blue">' Call the WindowSelect method of the clsSelect object</span>
	    oSelect.WindowSelect
	End Sub
End Class

Class clsSelect
	<span style="color:blue">' Declare the event objects</span>
	Private WithEvents oInteractEvents As InteractionEvents
	Private WithEvents oSelectEvents As SelectEvents
	Public ThisApplication As Inventor.Application
	
	<span style="color:blue">' Declare a flag that's used to determine if command prompts are shown as tooltips.</span>
	Private bTooltipEnabled As Boolean
	
	Public Function WindowSelect()
	    <span style="color:blue">' Create an InteractionEvents object.</span>
	    oInteractEvents = ThisApplication.CommandManager.CreateInteractionEvents
	
	    <span style="color:blue">' Ensure interaction is enabled.</span>
	    oInteractEvents.InteractionDisabled = False
	
	    <span style="color:blue">' Set a reference to the select events.</span>
	    oSelectEvents = oInteractEvents.SelectEvents
	
	    <span style="color:blue">' Set the filter for circular edges (this includes circular arcs).</span>
	    oSelectEvents.AddSelectionFilter( kPartEdgeCircularFilter)
	
	    oSelectEvents.WindowSelectEnabled = True
	
	    bTooltipEnabled = ThisApplication.GeneralOptions.ShowCommandPromptTooltips
	    ThisApplication.GeneralOptions.ShowCommandPromptTooltips = True
	
	    oInteractEvents.StatusBarText = "Window select. Esc to exit."
	
	    <span style="color:blue">' Start the InteractionEvents object.</span>
	    oInteractEvents.Start
	End Function
	
	Private Sub oInteractEvents_OnTerminate() Handles oInteractEvents.OnTerminate
	    <span style="color:blue">' Reset to original value</span>
	    ThisApplication.GeneralOptions.ShowCommandPromptTooltips = bTooltipEnabled
	
	    <span style="color:blue">' Clean up.</span>
	    oSelectEvents = Nothing
	    oInteractEvents = Nothing
	End Sub
	
	Private Sub oSelectEvents_OnPreSelectOnPreSelect(ByRef PreSelectEntity As Object, ByRef DoHighlight As Boolean, ByRef MorePreSelectEntities As Inventor.ObjectCollection, SelectionDevice As Inventor.SelectionDeviceEnum, ModelPosition As Inventor.Point, ViewPosition As Inventor.Point2d, View As Inventor.View) Handles oSelectEvents.OnPreSelect
	    <span style="color:blue">' Set a reference to the selected edge.</span>
	    <span style="color:blue">' Only circular edges can come through since the circular edge filter was set.</span>
	    Dim oEdge As Edge
	    oEdge = PreSelectEntity
	
	    <span style="color:blue">' Allow only fully circular edges to be picked.</span>
	    If Not oEdge.GeometryType = kCircleCurve Then
	      DoHighlight = False
	    End If
	End Sub
	
	Private Sub oSelectEvents_OnSelect(ByVal JustSelectedEntities As ObjectsEnumerator, ByVal SelectionDevice As SelectionDeviceEnum, ByVal ModelPosition As Point, ByVal ViewPosition As Point2d, ByVal View As View) Handles oSelectEvents.OnSelect
	    MsgBox ("Picked " & JustSelectedEntities.Count & " circular edges.")
	End Sub

End Class</pre>
      </div>
    </div><br><br><div id="CopyrightNotice" style="display:none;">
      <hr>
      <table width="100%" style="font-size:15">
        <tr>
          <td align="left">
                Â© Copyright 2025 Autodesk, Inc.
              </td>
          <td align="right"><a href="mailto:mfg.api.help.comments@autodesk.com?subject=Inventor%202026%20API%20Help%20(SelectEventsObject_WindowSelectEnabled%20Sample%20Help%20Topic)">Comment on this page.</a></td>
        </tr>
      </table>
    </div>
  </body>
</html>