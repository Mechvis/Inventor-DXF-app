<html>
  <head>
    <META http-equiv="Content-Type" content="text/html; charset=utf-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge">
    <meta name="topic-type" content="reference-adsk">
    <meta name="helpsystempath" content="/view/INVNTOR/2026/ENU/">
    <link rel="canonical" href="/cloudhelp/2026/ENU/Inventor-API/files/GraphicsDataSets_CreateColorSet_Sample.htm#!">
    <meta name="product" content="INVNTOR">
    <meta name="release" content="2026">
    <meta name="book" content="Developer">
    <meta name="component" content="Inventor-API">
    <meta name="created" content="">
    <meta name="user-comments" content="enable">
    <meta name="contextid" content="GraphicsDataSets_CreateColorSet_Sample">
    <meta name="description" content="Client Graphics - Vertex Color by Z Height Sample">
    <meta name="topicid" content="GraphicsDataSets_CreateColorSet_Sample">
    <meta name="experiencelevel" content="expert">
    <meta name="audience" content="programmer">
    <meta name="language" content="ENU">
    <title>Client Graphics - Vertex Color by Z Height</title>
    <link rel="stylesheet" href="../Style/jquery-ui.css"><script src="../Scripts/jquery-1.11.1.js"></script><script src="../Scripts/jquery-ui.js"></script><link rel="stylesheet" type="text/css" href="../Style/APIHelp-chm.css">
    <link rel="stylesheet" type="text/css" href="/view/clientframework/client.css"><script type="text/javascript" src="/view/clientframework/client.js"></script><script>
          // This turns on the display of the copyright notice which is not visible by default.
          // Because the head is removed when used in the web help, this function will only exists
          // when the chm help is being created.
          window.onload = function() {
          copyrightElement = document.getElementById('CopyrightNotice');
          copyrightElement.style.display = 'block';
          };
        </script><script>
          $(function() {
          $( "#tabs" ).tabs();
          });
        </script></head>
  <body><script>
          function copyCode(language) {
          // Get the pre element for the specified language.
          var preElement = document.getElementById(language);

          // Get the text from the element and copy it to the clipboard.
          var preText = preElement.innerText;

          var isIE = /*@cc_on!@*/false || !!document.documentMode;

          if (isIE)
          {
            window.clipboardData.setData('Text', preText);
          }
          else
          {
            navigator.clipboard.writeText(preText);
          }

          //alert("Code copied to clipboard");
          }
        </script><link rel="stylesheet" type="text/css" href="../Style/APIHelp.css">
    <h1 class="api">Client Graphics - Vertex Color by Z Height</h1>
    <h2 class="api">
          Description
        </h2>This sample demonstrates using client graphics and some other functions that help to support display control. It uses the currently active part and replaces the part display with a display where the part's color varies from blue to red where blue is assigned to the lowest Z portion of the part and red is assigned to the highest Z portion of the part. Areas in between are represented by a smooth blend of color from blue to red.<h2 class="api">
          Code Samples
        </h2>
    <div id="tabs">
      <ul>
        <li><a href="#VBA">VBA</a></li>
        <li><a href="#iLogic">iLogic</a></li>
      </ul>
      <div id="VBA" class="api-code">
        <table width="100%" style="font-size:15">
          <tr>
            <td align="right"><button onclick="copyCode('VBA_code')" class="copy-code">Copy Code</button></td>
          </tr>
        </table>
        <pre class="api-code" id="VBA_code">Public Sub ZHeightColors()
    <span style="color:blue">' Get the surface body from the active document.</span>
    Dim oPartDoc As PartDocument
    Set oPartDoc = ThisApplication.ActiveDocument
    Dim oSurfBody As SurfaceBody
    Set oSurfBody = oPartDoc.ComponentDefinition.SurfaceBodies.Item(1)
    Set oSurfBody = oPartDoc.ComponentDefinitions.Item(1).SurfaceBodies.Item(1)

    <span style="color:blue">' Delete the graphics data set and client graphics, if they exist.</span>
    Dim oDataSets As GraphicsDataSets
    On Error Resume Next
    Set oDataSets = oPartDoc.GraphicsDataSetsCollection.Item("MyTest")
    If Err.Number = 0 Then
        oDataSets.Delete
        oPartDoc.ComponentDefinition.ClientGraphicsCollection.Item("MyTest").Delete
        oSurfBody.Visible = True
        ThisApplication.ActiveView.Update
        Exit Sub
    End If
    On Error GoTo 0
    
    <span style="color:blue">' Determine the highest tolerance of the existing facet sets.</span>
    Dim ToleranceCount As Long
    Dim ExistingTolerances() As Double
    Call oSurfBody.GetExistingFacetTolerances(ToleranceCount, ExistingTolerances)
    
    Dim i As Long
    Dim BestTolerance As Double
    For i = 0 To ToleranceCount - 1
        If i = 0 Then
            BestTolerance = ExistingTolerances(i)
        ElseIf ExistingTolerances(i) &lt; BestTolerance Then
            BestTolerance = ExistingTolerances(i)
        End If
    Next
    
    <span style="color:blue">' Get a set of existing facets.</span>
    Dim iVertexCount As Long
    Dim iFacetCount As Long
    Dim adVertexCoords() As Double
    Dim adNormalVectors() As Double
    Dim aiVertexIndices() As Long
    Call oSurfBody.GetExistingFacets(BestTolerance, iVertexCount, iFacetCount, _
                                    adVertexCoords, adNormalVectors, aiVertexIndices)
                                                   
    <span style="color:blue">' Start a transaction.</span>
    Dim oTrans As Transaction
    Set oTrans = ThisApplication.TransactionManager.StartTransaction(oPartDoc, "Z Height Colors")

    <span style="color:blue">' Create the graphics data sets collection.</span>
    Set oDataSets = oPartDoc.GraphicsDataSetsCollection.Add("MyTest")
    
    <span style="color:blue">' Create the coordinate set and set it using the coordinates from the facets.</span>
    Dim oGraphicsCoordSet As GraphicsCoordinateSet
    Set oGraphicsCoordSet = oDataSets.CreateCoordinateSet(1)
    Call oGraphicsCoordSet.PutCoordinates(adVertexCoords)
    
    <span style="color:blue">' Create the index set and set it using the indices from the facets.</span>
    Dim oGraphicsIndexSet As GraphicsIndexSet
    Set oGraphicsIndexSet = oDataSets.CreateIndexSet(2)
    Call oGraphicsIndexSet.PutIndices(aiVertexIndices)
    
    <span style="color:blue">' Create the normal set and set it using the normals from the facets.</span>
    Dim oGraphicsNormalSet As GraphicsNormalSet
    Set oGraphicsNormalSet = oDataSets.CreateNormalSet(3)
    Call oGraphicsNormalSet.PutNormals(adNormalVectors)
    
    <span style="color:blue">' Determine the min-max range of the body in Z.</span>
    Dim dMinZ As Double
    dMinZ = oSurfBody.RangeBox.MinPoint.Z
    Dim dMaxZ As Double
    dMaxZ = oSurfBody.RangeBox.MaxPoint.Z
    Dim dHeightDifference As Double
    dHeightDifference = dMaxZ - dMinZ
    
    <span style="color:blue">' Allocate the array that will contain the color information.</span>
    <span style="color:blue">' This array contains RGB values for each vertex.</span>
    Dim abtColors() As Byte
    ReDim abtColors(0 To iVertexCount * 3 - 1) As Byte
    
    <span style="color:blue">' Load the array with color information for each vertex.</span>
    For i = 0 To iVertexCount - 1
        <span style="color:blue">' Get the Z height of the current vertex.</span>
        Dim dZValue As Double
        dZValue = adVertexCoords(i * 3 + 2)
        
        <span style="color:blue">' Set the color information for the current vertex.  It's computed by</span>
        <span style="color:blue">' determining the percentage of the total Z range of the body this vertex</span>
        <span style="color:blue">' is within.  A color between red and blue is computed based on this percentage.</span>
        <span style="color:blue">' Blue is at the minimum Z and Red is at the maximum Z with blending between.</span>
        abtColors(i * 3) = ((dZValue - dMinZ) / dHeightDifference) * 255
        abtColors(i * 3 + 1) = 0
        abtColors(i * 3 + 2) = ((dMaxZ - dZValue) / dHeightDifference) * 255
    Next
    
    <span style="color:blue">' Create the color set and set it using the array of rgb values just created.</span>
    Dim oGraphicsColorSet As GraphicsColorSet
    Set oGraphicsColorSet = oDataSets.CreateColorSet(4)
    Call oGraphicsColorSet.PutColors(abtColors)

    <span style="color:blue">' Create the client graphics collection.</span>
    Dim oClientGraphics As ClientGraphics
    Set oClientGraphics = oPartDoc.ComponentDefinition.ClientGraphicsCollection.Add("MyTest")
    
    <span style="color:blue">' Create a graphics node.</span>
    Dim oGraphicNode As GraphicsNode
    Set oGraphicNode = oClientGraphics.AddNode(1)
    
    <span style="color:blue">' Create the triangle graphics.</span>
    Dim oTriangles As TriangleGraphics
    Set oTriangles = oGraphicNode.AddTriangleGraphics
    
    <span style="color:blue">' Set various prroperties of the triangle graphics.</span>
    oTriangles.CoordinateSet = oGraphicsCoordSet
    oTriangles.CoordinateIndexSet = oGraphicsIndexSet
    oTriangles.NormalSet = oGraphicsNormalSet
    oTriangles.NormalBinding = kPerVertexNormals
    oTriangles.NormalIndexSet = oGraphicsIndexSet
    oTriangles.ColorSet = oGraphicsColorSet
    oTriangles.ColorBinding = kPerVertexColors
    oTriangles.ColorIndexSet = oGraphicsIndexSet

    <span style="color:blue">' Turn off the display of the body.</span>
    oSurfBody.Visible = False
    
    <span style="color:blue">' End the transaction.</span>
    oTrans.End

    <span style="color:blue">' Update the view.</span>
    ThisApplication.ActiveView.Update
End Sub</pre>
      </div>
      <div id="iLogic" class="api-code">
        <table width="100%" style="font-size:15">
          <tr>
            <td align="right"><button onclick="copyCode('iLogic_code')" class="copy-code">Copy Code</button></td>
          </tr>
        </table>
        <pre class="api-code" id="iLogic_code">    <span style="color:blue">' Get the surface body from the active document.</span>
    Dim oPartDoc As PartDocument
    oPartDoc = ThisApplication.ActiveDocument
    Dim oSurfBody As SurfaceBody
    oSurfBody = oPartDoc.ComponentDefinition.SurfaceBodies.Item(1)
    oSurfBody = oPartDoc.ComponentDefinitions.Item(1).SurfaceBodies.Item(1)

    <span style="color:blue">' Delete the graphics data set and client graphics, if they exist.</span>
    Dim oDataSets As GraphicsDataSets
    On Error Resume Next
    oDataSets = oPartDoc.GraphicsDataSetsCollection.Item("MyTest")
    If Err.Number = 0 Then
        oDataSets.Delete
        oPartDoc.ComponentDefinition.ClientGraphicsCollection.Item("MyTest").Delete
        oSurfBody.Visible = True
        ThisApplication.ActiveView.Update
        Exit Sub
    End If
    On Error GoTo 0
    
    <span style="color:blue">' Determine the highest tolerance of the existing facet sets.</span>
    Dim ToleranceCount As Long
    Dim ExistingTolerances() As Double = New Double() {}
    Call oSurfBody.GetExistingFacetTolerances(ToleranceCount, ExistingTolerances)
    
    Dim i As Long
    Dim BestTolerance As Double
    For i = 0 To ToleranceCount - 1
        If i = 0 Then
            BestTolerance = ExistingTolerances(i)
        ElseIf ExistingTolerances(i) &lt; BestTolerance Then
            BestTolerance = ExistingTolerances(i)
        End If
    Next
    
    <span style="color:blue">' Get a set of existing facets.</span>
    Dim iVertexCount As Integer
    Dim iFacetCount As Integer
    Dim adVertexCoords() As Double = New Double() {}
    Dim adNormalVectors() As Double = New Double() {}
    Dim aiVertexIndices() As Integer = New Integer() {}
    Call oSurfBody.GetExistingFacets(BestTolerance, iVertexCount, iFacetCount, _
                                    adVertexCoords, adNormalVectors, aiVertexIndices)
                                                   
    <span style="color:blue">' Start a transaction.</span>
    Dim oTrans As Transaction
    oTrans = ThisApplication.TransactionManager.StartTransaction(oPartDoc, "Z Height Colors")

    <span style="color:blue">' Create the graphics data sets collection.</span>
    oDataSets = oPartDoc.GraphicsDataSetsCollection.Add("MyTest")
    
    <span style="color:blue">' Create the coordinate set and set it using the coordinates from the facets.</span>
    Dim oGraphicsCoordSet As GraphicsCoordinateSet
    oGraphicsCoordSet = oDataSets.CreateCoordinateSet(1)
    Call oGraphicsCoordSet.PutCoordinates(adVertexCoords)
    
    <span style="color:blue">' Create the index set and set it using the indices from the facets.</span>
    Dim oGraphicsIndexSet As GraphicsIndexSet
    oGraphicsIndexSet = oDataSets.CreateIndexSet(2)
    Call oGraphicsIndexSet.PutIndices(aiVertexIndices)
    
    <span style="color:blue">' Create the normal set and set it using the normals from the facets.</span>
    Dim oGraphicsNormalSet As GraphicsNormalSet
    oGraphicsNormalSet = oDataSets.CreateNormalSet(3)
    Call oGraphicsNormalSet.PutNormals(adNormalVectors)
    
    <span style="color:blue">' Determine the min-max range of the body in Z.</span>
    Dim dMinZ As Double
    dMinZ = oSurfBody.RangeBox.MinPoint.Z
    Dim dMaxZ As Double
    dMaxZ = oSurfBody.RangeBox.MaxPoint.Z
    Dim dHeightDifference As Double
    dHeightDifference = dMaxZ - dMinZ
    
    <span style="color:blue">' Allocate the array that will contain the color information.</span>
    <span style="color:blue">' This array contains RGB values for each vertex.</span>
    Dim abtColors(0 To iVertexCount * 3 - 1) As Byte
    
    <span style="color:blue">' Load the array with color information for each vertex.</span>
    For i = 0 To iVertexCount - 1
        <span style="color:blue">' Get the Z height of the current vertex.</span>
        Dim dZValue As Double
        dZValue = adVertexCoords(i * 3 + 2)
        
        <span style="color:blue">' Set the color information for the current vertex.  It's computed by</span>
        <span style="color:blue">' determining the percentage of the total Z range of the body this vertex</span>
        <span style="color:blue">' is within.  A color between red and blue is computed based on this percentage.</span>
        <span style="color:blue">' Blue is at the minimum Z and Red is at the maximum Z with blending between.</span>
        abtColors(i * 3) = ((dZValue - dMinZ) / dHeightDifference) * 255
        abtColors(i * 3 + 1) = 0
        abtColors(i * 3 + 2) = ((dMaxZ - dZValue) / dHeightDifference) * 255
    Next
    
    <span style="color:blue">' Create the color set and set it using the array of rgb values just created.</span>
    Dim oGraphicsColorSet As GraphicsColorSet
    oGraphicsColorSet = oDataSets.CreateColorSet(4)
    Call oGraphicsColorSet.PutColors(abtColors)

    <span style="color:blue">' Create the client graphics collection.</span>
    Dim oClientGraphics As ClientGraphics
    oClientGraphics = oPartDoc.ComponentDefinition.ClientGraphicsCollection.Add("MyTest")
    
    <span style="color:blue">' Create a graphics node.</span>
    Dim oGraphicNode As GraphicsNode
    oGraphicNode = oClientGraphics.AddNode(1)
    
    <span style="color:blue">' Create the triangle graphics.</span>
    Dim oTriangles As TriangleGraphics
    oTriangles = oGraphicNode.AddTriangleGraphics
    
    <span style="color:blue">' Set various prroperties of the triangle graphics.</span>
    oTriangles.CoordinateSet = oGraphicsCoordSet
    oTriangles.CoordinateIndexSet = oGraphicsIndexSet
    oTriangles.NormalSet = oGraphicsNormalSet
    oTriangles.NormalBinding = kPerVertexNormals
    oTriangles.NormalIndexSet = oGraphicsIndexSet
    oTriangles.ColorSet = oGraphicsColorSet
    oTriangles.ColorBinding = kPerVertexColors
    oTriangles.ColorIndexSet = oGraphicsIndexSet

    <span style="color:blue">' Turn off the display of the body.</span>
    oSurfBody.Visible = False
    
    <span style="color:blue">' End the transaction.</span>
    oTrans.End

    <span style="color:blue">' Update the view.</span>
    ThisApplication.ActiveView.Update</pre>
      </div>
    </div><br><br><div id="CopyrightNotice" style="display:none;">
      <hr>
      <table width="100%" style="font-size:15">
        <tr>
          <td align="left">
                Â© Copyright 2025 Autodesk, Inc.
              </td>
          <td align="right"><a href="mailto:mfg.api.help.comments@autodesk.com?subject=Inventor%202026%20API%20Help%20(GraphicsDataSets_CreateColorSet%20Sample%20Help%20Topic)">Comment on this page.</a></td>
        </tr>
      </table>
    </div>
  </body>
</html>