<html>
  <head>
    <META http-equiv="Content-Type" content="text/html; charset=utf-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge">
    <meta name="topic-type" content="reference-adsk">
    <meta name="helpsystempath" content="/view/INVNTOR/2026/ENU/">
    <link rel="canonical" href="/cloudhelp/2026/ENU/Inventor-API/files/FilletFeature2_Sample.htm#!">
    <meta name="product" content="INVNTOR">
    <meta name="release" content="2026">
    <meta name="book" content="Developer">
    <meta name="component" content="Inventor-API">
    <meta name="created" content="">
    <meta name="user-comments" content="enable">
    <meta name="contextid" content="FilletFeature2_Sample">
    <meta name="description" content="Fillet Feature (Complex) Sample">
    <meta name="topicid" content="FilletFeature2_Sample">
    <meta name="experiencelevel" content="expert">
    <meta name="audience" content="programmer">
    <meta name="language" content="ENU">
    <title>Fillet Feature (Complex)</title>
    <link rel="stylesheet" href="../Style/jquery-ui.css"><script src="../Scripts/jquery-1.11.1.js"></script><script src="../Scripts/jquery-ui.js"></script><link rel="stylesheet" type="text/css" href="../Style/APIHelp-chm.css">
    <link rel="stylesheet" type="text/css" href="/view/clientframework/client.css"><script type="text/javascript" src="/view/clientframework/client.js"></script><script>
          // This turns on the display of the copyright notice which is not visible by default.
          // Because the head is removed when used in the web help, this function will only exists
          // when the chm help is being created.
          window.onload = function() {
          copyrightElement = document.getElementById('CopyrightNotice');
          copyrightElement.style.display = 'block';
          };
        </script><script>
          $(function() {
          $( "#tabs" ).tabs();
          });
        </script></head>
  <body><script>
          function copyCode(language) {
          // Get the pre element for the specified language.
          var preElement = document.getElementById(language);

          // Get the text from the element and copy it to the clipboard.
          var preText = preElement.innerText;

          var isIE = /*@cc_on!@*/false || !!document.documentMode;

          if (isIE)
          {
            window.clipboardData.setData('Text', preText);
          }
          else
          {
            navigator.clipboard.writeText(preText);
          }

          //alert("Code copied to clipboard");
          }
        </script><link rel="stylesheet" type="text/css" href="../Style/APIHelp.css">
    <h1 class="api">Fillet Feature (Complex)</h1>
    <h2 class="api">
          Description
        </h2>This sample demonstrates creating a complex fillet. The result in this case has several different constant radii fillets and two edges that use variable radius, with one of these having a different radius defined along the edge.<h2 class="api">
          Code Samples
        </h2>
    <div id="tabs">
      <ul>
        <li><a href="#VBA">VBA</a></li>
        <li><a href="#iLogic">iLogic</a></li>
      </ul>
      <div id="VBA" class="api-code">
        <table width="100%" style="font-size:15">
          <tr>
            <td align="right"><button onclick="copyCode('VBA_code')" class="copy-code">Copy Code</button></td>
          </tr>
        </table>
        <pre class="api-code" id="VBA_code">Public Sub CreateFilletComplex()
    <span style="color:blue">' Create a new Part document.</span>
    Dim oPartDoc As PartDocument
    Set oPartDoc = ThisApplication.Documents.Add(kPartDocumentObject, _
                 ThisApplication.FileManager.GetTemplateFile(kPartDocumentObject))
    
    <span style="color:blue">' Set a reference to the compdef.</span>
    Dim oCompDef As PartComponentDefinition
    Set oCompDef = oPartDoc.ComponentDefinition
    
    <span style="color:blue">' Create a sketch on the xy work plane.</span>
    Dim oSketch As PlanarSketch
    Set oSketch = oCompDef.Sketches.Add(oCompDef.WorkPlanes.Item(3))
    
    <span style="color:blue">' Draw a rectangle.</span>
    Dim oEnts As SketchEntitiesEnumerator
    Set oEnts = oSketch.SketchLines.AddAsTwoPointRectangle( _
                            ThisApplication.TransientGeometry.CreatePoint2d(-6, -4), _
                            ThisApplication.TransientGeometry.CreatePoint2d(6, 4))

    Dim oProfile As Profile
    Set oProfile = oSketch.Profiles.AddForSolid
    
    <span style="color:blue">' Create an extrusion.</span>
    Dim oExtrudeDef As ExtrudeDefinition
    Set oExtrudeDef = oCompDef.Features.ExtrudeFeatures.CreateExtrudeDefinition(oProfile, kJoinOperation)
    Call oExtrudeDef.SetDistanceExtent(8, kSymmetricExtentDirection)
    Dim oExtrude As ExtrudeFeature
    Set oExtrude = oCompDef.Features.ExtrudeFeatures.Add(oExtrudeDef)

    <span style="color:blue">' Create an EdgeCollection object to use for inputting the edges for fillets.</span>
    Dim oEdgeCollection As EdgeCollection
    Set oEdgeCollection = ThisApplication.TransientObjects.CreateEdgeCollection
    
    <span style="color:blue">' Add all of the edges of the start face to the edge collection.</span>
    Dim oEdge As Edge
    For Each oEdge In oExtrude.StartFaces.Item(1).Edges
        oEdgeCollection.Add oEdge
    Next

    <span style="color:blue">' Obtain a FilletDefinition object to use in defining the various inputs to create the fillet.</span>
    Dim oFilletDef As FilletDefinition
    Set oFilletDef = oCompDef.Features.FilletFeatures.CreateFilletDefinition

    <span style="color:blue">' Create the first edge set.</span>
    Call oFilletDef.AddConstantRadiusEdgeSet(oEdgeCollection, 1.5)
    
    <span style="color:blue">' Reinitialize the edge collection.</span>
    Set oEdgeCollection = ThisApplication.TransientObjects.CreateEdgeCollection
    
    <span style="color:blue">' Add all of the edges of the end face to the edge collection.</span>
    For Each oEdge In oExtrude.EndFaces.Item(1).Edges
        oEdgeCollection.Add oEdge
    Next
    
    <span style="color:blue">' Create the first edge set.</span>
    Call oFilletDef.AddConstantRadiusEdgeSet(oEdgeCollection, 1)
    
    <span style="color:blue">' Find the edges that go between the start and end faces by checking to</span>
    <span style="color:blue">' see if they're parallel to the Z axis.</span>
    Dim oZVector As UnitVector
    Set oZVector = ThisApplication.TransientGeometry.CreateUnitVector(0, 0, 1)
    Dim oSideEdges(1 To 4) As Edge
    Dim EdgeCount As Long
    EdgeCount = 0
    For Each oEdge In oCompDef.SurfaceBodies.Item(1).Edges
        <span style="color:blue">' In this case we know all the edges are linear.</span>
        Dim oLine As LineSegment
        Set oLine = oEdge.Geometry
        
        If oLine.Direction.IsParallelTo(oZVector) Then
            EdgeCount = EdgeCount + 1
            Set oSideEdges(EdgeCount) = oEdge
        End If
    Next
    
    <span style="color:blue">' Add the first two edges to a constant radius edge set.</span>
    Set oEdgeCollection = ThisApplication.TransientObjects.CreateEdgeCollection
    oEdgeCollection.Add oSideEdges(1)
    oEdgeCollection.Add oSideEdges(2)
    Call oFilletDef.AddConstantRadiusEdgeSet(oEdgeCollection, 0.5)
    
    <span style="color:blue">' Create a variable radius edge set for the third edge.</span>
    Set oEdgeCollection = ThisApplication.TransientObjects.CreateEdgeCollection
    oEdgeCollection.Add oSideEdges(3)
    Call oFilletDef.AddVariableRadiusEdgeSet(oEdgeCollection, 1, 2)
    
    <span style="color:blue">' Create a variable radius edge with a different internal radius for the fourth edge.</span>
    Set oEdgeCollection = ThisApplication.TransientObjects.CreateEdgeCollection
    oEdgeCollection.Add oSideEdges(4)
    Dim oVarRadiusEdgeSet As FilletVariableRadiusEdgeSet
    Set oVarRadiusEdgeSet = oFilletDef.AddVariableRadiusEdgeSet(oEdgeCollection, 0.5, 1.5)
    Call oVarRadiusEdgeSet.AddIntermediateRadius(oSideEdges(4), 3, 0.5)
        
    Dim oFillet As FilletFeature
    Set oFillet = oCompDef.Features.FilletFeatures.Add(oFilletDef, False)
End Sub</pre>
      </div>
      <div id="iLogic" class="api-code">
        <table width="100%" style="font-size:15">
          <tr>
            <td align="right"><button onclick="copyCode('iLogic_code')" class="copy-code">Copy Code</button></td>
          </tr>
        </table>
        <pre class="api-code" id="iLogic_code">    <span style="color:blue">' Create a new Part document.</span>
    Dim oPartDoc As PartDocument
    oPartDoc = ThisApplication.Documents.Add(kPartDocumentObject, _
                 ThisApplication.FileManager.GetTemplateFile(kPartDocumentObject))
    
    <span style="color:blue">' Set a reference to the compdef.</span>
    Dim oCompDef As PartComponentDefinition
    oCompDef = oPartDoc.ComponentDefinition
    
    <span style="color:blue">' Create a sketch on the xy work plane.</span>
    Dim oSketch As PlanarSketch
    oSketch = oCompDef.Sketches.Add(oCompDef.WorkPlanes.Item(3))
    
    <span style="color:blue">' Draw a rectangle.</span>
    Dim oEnts As SketchEntitiesEnumerator
    oEnts = oSketch.SketchLines.AddAsTwoPointRectangle( _
                            ThisApplication.TransientGeometry.CreatePoint2d(-6, -4), _
                            ThisApplication.TransientGeometry.CreatePoint2d(6, 4))

    Dim oProfile As Profile
    oProfile = oSketch.Profiles.AddForSolid
    
    <span style="color:blue">' Create an extrusion.</span>
    Dim oExtrudeDef As ExtrudeDefinition
    oExtrudeDef = oCompDef.Features.ExtrudeFeatures.CreateExtrudeDefinition(oProfile, kJoinOperation)
    Call oExtrudeDef.SetDistanceExtent(8, kSymmetricExtentDirection)
    Dim oExtrude As ExtrudeFeature
    oExtrude = oCompDef.Features.ExtrudeFeatures.Add(oExtrudeDef)

    <span style="color:blue">' Create an EdgeCollection object to use for inputting the edges for fillets.</span>
    Dim oEdgeCollection As EdgeCollection
    oEdgeCollection = ThisApplication.TransientObjects.CreateEdgeCollection
    
    <span style="color:blue">' Add all of the edges of the start face to the edge collection.</span>
    Dim oEdge As Edge
    For Each oEdge In oExtrude.StartFaces.Item(1).Edges
        oEdgeCollection.Add(oEdge)
    Next

    <span style="color:blue">' Obtain a FilletDefinition object to use in defining the various inputs to create the fillet.</span>
    Dim oFilletDef As FilletDefinition
    oFilletDef = oCompDef.Features.FilletFeatures.CreateFilletDefinition

    <span style="color:blue">' Create the first edge set.</span>
    Call oFilletDef.AddConstantRadiusEdgeSet(oEdgeCollection, 1.5)
    
    <span style="color:blue">' Reinitialize the edge collection.</span>
    oEdgeCollection = ThisApplication.TransientObjects.CreateEdgeCollection
    
    <span style="color:blue">' Add all of the edges of the end face to the edge collection.</span>
    For Each oEdge In oExtrude.EndFaces.Item(1).Edges
        oEdgeCollection.Add(oEdge)
    Next
    
    <span style="color:blue">' Create the first edge set.</span>
    Call oFilletDef.AddConstantRadiusEdgeSet(oEdgeCollection, 1)
    
    <span style="color:blue">' Find the edges that go between the start and end faces by checking to</span>
    <span style="color:blue">' see if they're parallel to the Z axis.</span>
    Dim oZVector As UnitVector
    oZVector = ThisApplication.TransientGeometry.CreateUnitVector(0, 0, 1)
    Dim oSideEdges(0 To 3) As Edge
    Dim EdgeCount As Long
    EdgeCount = 0
    For Each oEdge In oCompDef.SurfaceBodies.Item(1).Edges
        <span style="color:blue">' In this case we know all the edges are linear.</span>
        Dim oLine As LineSegment
        oLine = oEdge.Geometry
        
        If oLine.Direction.IsParallelTo(oZVector) Then
            EdgeCount = EdgeCount + 1
            oSideEdges(EdgeCount - 1) = oEdge
        End If
    Next
    
    <span style="color:blue">' Add the first two edges to a constant radius edge set.</span>
    oEdgeCollection = ThisApplication.TransientObjects.CreateEdgeCollection
    oEdgeCollection.Add(oSideEdges(0))
    oEdgeCollection.Add(oSideEdges(1))
    Call oFilletDef.AddConstantRadiusEdgeSet(oEdgeCollection, 0.5)
    
    <span style="color:blue">' Create a variable radius edge set for the third edge.</span>
    oEdgeCollection = ThisApplication.TransientObjects.CreateEdgeCollection
    oEdgeCollection.Add(oSideEdges(2))
    Call oFilletDef.AddVariableRadiusEdgeSet(oEdgeCollection, 1, 2)
    
    <span style="color:blue">' Create a variable radius edge with a different internal radius for the fourth edge.</span>
    oEdgeCollection = ThisApplication.TransientObjects.CreateEdgeCollection
    oEdgeCollection.Add(oSideEdges(3))
    Dim oVarRadiusEdgeSet As FilletVariableRadiusEdgeSet
    oVarRadiusEdgeSet = oFilletDef.AddVariableRadiusEdgeSet(oEdgeCollection, 0.5, 1.5)
    Call oVarRadiusEdgeSet.AddIntermediateRadius(oSideEdges(3), 3, 0.5)
        
    Dim oFillet As FilletFeature
    oFillet = oCompDef.Features.FilletFeatures.Add(oFilletDef, False)</pre>
      </div>
    </div><br><br><div id="CopyrightNotice" style="display:none;">
      <hr>
      <table width="100%" style="font-size:15">
        <tr>
          <td align="left">
                Â© Copyright 2025 Autodesk, Inc.
              </td>
          <td align="right"><a href="mailto:mfg.api.help.comments@autodesk.com?subject=Inventor%202026%20API%20Help%20(FilletFeature2%20Sample%20Help%20Topic)">Comment on this page.</a></td>
        </tr>
      </table>
    </div>
  </body>
</html>