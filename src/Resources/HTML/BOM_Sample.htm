<html>
  <head>
    <META http-equiv="Content-Type" content="text/html; charset=utf-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge">
    <meta name="topic-type" content="reference-adsk">
    <meta name="helpsystempath" content="/view/INVNTOR/2026/ENU/">
    <link rel="canonical" href="/cloudhelp/2026/ENU/Inventor-API/files/BOM_Sample.htm#!">
    <meta name="product" content="INVNTOR">
    <meta name="release" content="2026">
    <meta name="book" content="Developer">
    <meta name="component" content="Inventor-API">
    <meta name="created" content="">
    <meta name="user-comments" content="enable">
    <meta name="contextid" content="BOM_Sample">
    <meta name="description" content="Using the BOM APIs Sample">
    <meta name="topicid" content="BOM_Sample">
    <meta name="experiencelevel" content="expert">
    <meta name="audience" content="programmer">
    <meta name="language" content="ENU">
    <title>Using the BOM APIs</title>
    <link rel="stylesheet" href="../Style/jquery-ui.css"><script src="../Scripts/jquery-1.11.1.js"></script><script src="../Scripts/jquery-ui.js"></script><link rel="stylesheet" type="text/css" href="../Style/APIHelp-chm.css">
    <link rel="stylesheet" type="text/css" href="/view/clientframework/client.css"><script type="text/javascript" src="/view/clientframework/client.js"></script><script>
          // This turns on the display of the copyright notice which is not visible by default.
          // Because the head is removed when used in the web help, this function will only exists
          // when the chm help is being created.
          window.onload = function() {
          copyrightElement = document.getElementById('CopyrightNotice');
          copyrightElement.style.display = 'block';
          };
        </script><script>
          $(function() {
          $( "#tabs" ).tabs();
          });
        </script></head>
  <body><script>
          function copyCode(language) {
          // Get the pre element for the specified language.
          var preElement = document.getElementById(language);

          // Get the text from the element and copy it to the clipboard.
          var preText = preElement.innerText;

          var isIE = /*@cc_on!@*/false || !!document.documentMode;

          if (isIE)
          {
            window.clipboardData.setData('Text', preText);
          }
          else
          {
            navigator.clipboard.writeText(preText);
          }

          //alert("Code copied to clipboard");
          }
        </script><link rel="stylesheet" type="text/css" href="../Style/APIHelp.css">
    <h1 class="api">Using the BOM APIs</h1>
    <h2 class="api">
          Description
        </h2>This sample demonstrates the Bill of Materials API functionality in assemblies.<h2 class="api">
          Code Samples
        </h2>
    <div id="tabs">
      <ul>
        <li><a href="#VBA">VBA</a></li>
        <li><a href="#iLogic">iLogic</a></li>
      </ul>
      <div id="VBA" class="api-code">Have an assembly document open and run the following sample.<table width="100%" style="font-size:15">
          <tr>
            <td align="right"><button onclick="copyCode('VBA_code')" class="copy-code">Copy Code</button></td>
          </tr>
        </table>
        <pre class="api-code" id="VBA_code">Public Sub BOMQuery()
    <span style="color:blue">' Set a reference to the assembly document.</span>
    <span style="color:blue">' This assumes an assembly document is active.</span>
    Dim oDoc As AssemblyDocument
    Set oDoc = ThisApplication.ActiveDocument

    Dim FirstLevelOnly As Boolean
    If MsgBox("First level only?", vbYesNo) = vbYes Then
        FirstLevelOnly = True
    Else
        FirstLevelOnly = False
    End If
    
    <span style="color:blue">' Set a reference to the BOM</span>
    Dim oBOM As BOM
    Set oBOM = oDoc.ComponentDefinition.BOM
    
    <span style="color:blue">' Set whether first level only or all levels.</span>
    If FirstLevelOnly Then
        oBOM.StructuredViewFirstLevelOnly = True
    Else
        oBOM.StructuredViewFirstLevelOnly = False
    End If
    
    <span style="color:blue">' Make sure that the structured view is enabled.</span>
    oBOM.StructuredViewEnabled = True
    
    <span style="color:blue">'Set a reference to the "Structured" BOMView</span>
    Dim oBOMView As BOMView
    Set oBOMView = oBOM.BOMViews.Item("Structured")
        
    Debug.Print "Item"; Tab(15); "Quantity"; Tab(30); "Part Number"; Tab(70); "Description"
    Debug.Print "----------------------------------------------------------------------------------"

    <span style="color:blue">'Initialize the tab for ItemNumber</span>
    Dim ItemTab As Long
    ItemTab = -3
    Call QueryBOMRowProperties(oBOMView.BOMRows, ItemTab)
End Sub

Private Sub QueryBOMRowProperties(oBOMRows As BOMRowsEnumerator, ItemTab As Long)
    ItemTab = ItemTab + 3
    <span style="color:blue">' Iterate through the contents of the BOM Rows.</span>
    Dim i As Long
    For i = 1 To oBOMRows.Count
        <span style="color:blue">' Get the current row.</span>
        Dim oRow As BOMRow
        Set oRow = oBOMRows.Item(i)

        <span style="color:blue">'Set a reference to the primary ComponentDefinition of the row</span>
        Dim oCompDef As ComponentDefinition
        Set oCompDef = oRow.ComponentDefinitions.Item(1)

        Dim oPartNumProperty As Property
        Dim oDescripProperty As Property

        If Typeof oCompDef Is VirtualComponentDefinition Then
            <span style="color:blue">'Get the file property that contains the "Part Number"</span>
            <span style="color:blue">'The file property is obtained from the virtual component definition</span>
            Set oPartNumProperty = oCompDef.PropertySets _
                .Item("Design Tracking Properties").Item("Part Number")

            <span style="color:blue">'Get the file property that contains the "Description"</span>
            Set oDescripProperty = oCompDef.PropertySets _
                .Item("Design Tracking Properties").Item("Description")

            Debug.Print Tab(ItemTab); oRow.ItemNumber; Tab(17); oRow.ItemQuantity; Tab(30); _
                oPartNumProperty.Value; Tab(70); oDescripProperty.Value
        Else
            <span style="color:blue">'Get the file property that contains the "Part Number"</span>
            <span style="color:blue">'The file property is obtained from the parent</span>
            <span style="color:blue">'document of the associated ComponentDefinition.</span>
            Set oPartNumProperty = oCompDef.Document.PropertySets _
                .Item("Design Tracking Properties").Item("Part Number")

            <span style="color:blue">'Get the file property that contains the "Description"</span>
            Set oDescripProperty = oCompDef.Document.PropertySets _
                .Item("Design Tracking Properties").Item("Description")

            Debug.Print Tab(ItemTab); oRow.ItemNumber; Tab(17); oRow.ItemQuantity; Tab(30); _
                oPartNumProperty.Value; Tab(70); oDescripProperty.Value
            
            <span style="color:blue">'Recursively iterate child rows if present.</span>
            If Not oRow.ChildRows Is Nothing Then
                Call QueryBOMRowProperties(oRow.ChildRows, ItemTab)
            End If
        End If
    Next
    ItemTab = ItemTab - 3
End Sub</pre>
      </div>
      <div id="iLogic" class="api-code">Have an assembly document open and run the following sample.<table width="100%" style="font-size:15">
          <tr>
            <td align="right"><button onclick="copyCode('iLogic_code')" class="copy-code">Copy Code</button></td>
          </tr>
        </table>
        <pre class="api-code" id="iLogic_code">Sub Main
    <span style="color:blue">' Set a reference to the assembly document.</span>
    <span style="color:blue">' This assumes an assembly document is active.</span>
    Dim oDoc As AssemblyDocument
    oDoc = ThisApplication.ActiveDocument

    Dim FirstLevelOnly As Boolean
    If MsgBox("First level only?", vbYesNo) = vbYes Then
        FirstLevelOnly = True
    Else
        FirstLevelOnly = False
    End If
    
    <span style="color:blue">' Set a reference to the BOM</span>
    Dim oBOM As BOM
    oBOM = oDoc.ComponentDefinition.BOM
    
    <span style="color:blue">' Set whether first level only or all levels.</span>
    If FirstLevelOnly Then
        oBOM.StructuredViewFirstLevelOnly = True
    Else
        oBOM.StructuredViewFirstLevelOnly = False
    End If
    
    <span style="color:blue">' Make sure that the structured view is enabled.</span>
    oBOM.StructuredViewEnabled = True
    
    <span style="color:blue">'Set a reference to the "Structured" BOMView</span>
    Dim oBOMView As BOMView
    oBOMView = oBOM.BOMViews.Item("Structured")
        
    Logger.Info("Item".PadRight(15) + "Quantity".PadRight(15) + "Part Number".PadRight(40) + "Description")

    Logger.Info("----------------------------------------------------------------------------------")


    <span style="color:blue">'Initialize the tab for ItemNumber</span>
    Dim ItemTab As Long
    ItemTab = -3
    Call QueryBOMRowProperties(oBOMView.BOMRows, ItemTab)
End Sub

Private Sub QueryBOMRowProperties(oBOMRows As BOMRowsEnumerator, ItemTab As Long)
    ItemTab = ItemTab + 3
    <span style="color:blue">' Iterate through the contents of the BOM Rows.</span>
    Dim i As Long
    For i = 1 To oBOMRows.Count
        <span style="color:blue">' Get the current row.</span>
        Dim oRow As BOMRow
        oRow = oBOMRows.Item(i)

        <span style="color:blue">'Set a reference to the primary ComponentDefinition of the row</span>
        Dim oCompDef As ComponentDefinition
        oCompDef = oRow.ComponentDefinitions.Item(1)

        Dim oPartNumProperty As Inventor.Property
        Dim oDescripProperty As Inventor.Property

        If Typeof oCompDef Is VirtualComponentDefinition Then
            <span style="color:blue">'Get the file property that contains the "Part Number"</span>
            <span style="color:blue">'The file property is obtained from the virtual component definition</span>
            oPartNumProperty = oCompDef.PropertySets _
                .Item("Design Tracking Properties").Item("Part Number")
			
            <span style="color:blue">'Get the file property that contains the "Description"</span>
            oDescripProperty = oCompDef.PropertySets _
                .Item("Design Tracking Properties").Item("Description")

            Logger.Info("".PadRight(ItemTab) + oRow.ItemNumber.PadRight(Max(15-ItemTab,0)) + CStr(oRow.ItemQuantity).PadRight(15) + oPartNumProperty.Value.PadRight(40) + oDescripProperty.Value)
        Else
            <span style="color:blue">'Get the file property that contains the "Part Number"</span>
            <span style="color:blue">'The file property is obtained from the parent</span>
            <span style="color:blue">'document of the associated ComponentDefinition.</span>
            oPartNumProperty = oCompDef.Document.PropertySets _
                .Item("Design Tracking Properties").Item("Part Number")

            <span style="color:blue">'Get the file property that contains the "Description"</span>
            oDescripProperty = oCompDef.Document.PropertySets _
                .Item("Design Tracking Properties").Item("Description")

            Logger.Info("".PadRight(ItemTab) + oRow.ItemNumber.PadRight(Max(15-ItemTab, 0)) + CStr(oRow.ItemQuantity).PadRight(15) + oPartNumProperty.Value.PadRight(40) + oDescripProperty.Value)

            <span style="color:blue">'Recursively iterate child rows if present.</span>
            If Not oRow.ChildRows Is Nothing Then
                Call QueryBOMRowProperties(oRow.ChildRows, ItemTab)
            End If
        End If
    Next
    ItemTab = ItemTab - 3
End Sub</pre>
      </div>
    </div><br><br><div id="CopyrightNotice" style="display:none;">
      <hr>
      <table width="100%" style="font-size:15">
        <tr>
          <td align="left">
                Â© Copyright 2025 Autodesk, Inc.
              </td>
          <td align="right"><a href="mailto:mfg.api.help.comments@autodesk.com?subject=Inventor%202026%20API%20Help%20(BOM%20Sample%20Help%20Topic)">Comment on this page.</a></td>
        </tr>
      </table>
    </div>
  </body>
</html>