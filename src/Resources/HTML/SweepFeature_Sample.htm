<html>
  <head>
    <META http-equiv="Content-Type" content="text/html; charset=utf-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge">
    <meta name="topic-type" content="reference-adsk">
    <meta name="helpsystempath" content="/view/INVNTOR/2026/ENU/">
    <link rel="canonical" href="/cloudhelp/2026/ENU/Inventor-API/files/SweepFeature_Sample.htm#!">
    <meta name="product" content="INVNTOR">
    <meta name="release" content="2026">
    <meta name="book" content="Developer">
    <meta name="component" content="Inventor-API">
    <meta name="created" content="">
    <meta name="user-comments" content="enable">
    <meta name="contextid" content="SweepFeature_Sample">
    <meta name="description" content="Sweep Feature Add Sample">
    <meta name="topicid" content="SweepFeature_Sample">
    <meta name="experiencelevel" content="expert">
    <meta name="audience" content="programmer">
    <meta name="language" content="ENU">
    <title>Sweep Feature Add</title>
    <link rel="stylesheet" href="../Style/jquery-ui.css"><script src="../Scripts/jquery-1.11.1.js"></script><script src="../Scripts/jquery-ui.js"></script><link rel="stylesheet" type="text/css" href="../Style/APIHelp-chm.css">
    <link rel="stylesheet" type="text/css" href="/view/clientframework/client.css"><script type="text/javascript" src="/view/clientframework/client.js"></script><script>
          // This turns on the display of the copyright notice which is not visible by default.
          // Because the head is removed when used in the web help, this function will only exists
          // when the chm help is being created.
          window.onload = function() {
          copyrightElement = document.getElementById('CopyrightNotice');
          copyrightElement.style.display = 'block';
          };
        </script><script>
          $(function() {
          $( "#tabs" ).tabs();
          });
        </script></head>
  <body><script>
          function copyCode(language) {
          // Get the pre element for the specified language.
          var preElement = document.getElementById(language);

          // Get the text from the element and copy it to the clipboard.
          var preText = preElement.innerText;

          var isIE = /*@cc_on!@*/false || !!document.documentMode;

          if (isIE)
          {
            window.clipboardData.setData('Text', preText);
          }
          else
          {
            navigator.clipboard.writeText(preText);
          }

          //alert("Code copied to clipboard");
          }
        </script><link rel="stylesheet" type="text/css" href="../Style/APIHelp.css">
    <h1 class="api">Sweep Feature Add</h1>
    <h2 class="api">
          Description
        </h2>This sample demonstrates the creation of a sweep feature. The profile is a circle, but the path is made up of a 3D sketch and a 2D sketch.<h2 class="api">
          Code Samples
        </h2>
    <div id="tabs">
      <ul>
        <li><a href="#VBA">VBA</a></li>
        <li><a href="#iLogic">iLogic</a></li>
      </ul>
      <div id="VBA" class="api-code">To run the sample have a part document open.<table width="100%" style="font-size:15">
          <tr>
            <td align="right"><button onclick="copyCode('VBA_code')" class="copy-code">Copy Code</button></td>
          </tr>
        </table>
        <pre class="api-code" id="VBA_code">Public Sub SweepFeature()
    <span style="color:blue">' Set a reference to the currently active document.</span>
    <span style="color:blue">' This assumes that it is a part document.</span>
    Dim oPartDoc As PartDocument
    Set oPartDoc = ThisApplication.ActiveDocument

    <span style="color:blue">' Set a reference to the component definition.</span>
    Dim oCompDef As PartComponentDefinition
    Set oCompDef = oPartDoc.ComponentDefinition

    <span style="color:blue">' Set a reference to the transient geometry object.</span>
    Dim oTG As TransientGeometry
    Set oTG = ThisApplication.TransientGeometry

    <span style="color:blue">' Create some workpoints that will be used for the 3D sketch.</span>
    Dim oWPs(1 To 5) As WorkPoint
    Set oWPs(1) = oCompDef.WorkPoints.AddFixed(oTG.CreatePoint(0, 0, 0))
    Set oWPs(2) = oCompDef.WorkPoints.AddFixed(oTG.CreatePoint(3, 0, 0))
    Set oWPs(3) = oCompDef.WorkPoints.AddFixed(oTG.CreatePoint(3, 4, 0))
    Set oWPs(4) = oCompDef.WorkPoints.AddFixed(oTG.CreatePoint(3, 4, 2))
    Set oWPs(5) = oCompDef.WorkPoints.AddFixed(oTG.CreatePoint(6, 4, 2))

    <span style="color:blue">' Create a new 3D Sketch.</span>
    Dim oSketch3D As Sketch3D
    Set oSketch3D = oPartDoc.ComponentDefinition.Sketches3D.Add

    <span style="color:blue">' Draw 3D lines. The first line is drawn between two of the work points.</span>
    Dim oLine As SketchLine3D
    Set oLine = oSketch3D.SketchLines3D.AddByTwoPoints(oWPs(1), oWPs(2), True, 1)

    <span style="color:blue">' This second and subsequent lines are drawn between a 3D sketch point and a work point.</span>
    <span style="color:blue">' The work point is obtained from the previous line. Because the two lines will share</span>
    <span style="color:blue">' this 3D sketch point, Inventor will treat them as connected lines when creating any</span>
    <span style="color:blue">' paths.</span>
    Set oLine = oSketch3D.SketchLines3D.AddByTwoPoints(oLine.EndPoint, oWPs(3), True, 1)
    Set oLine = oSketch3D.SketchLines3D.AddByTwoPoints(oLine.EndPoint, oWPs(4), True, 0.75)
    Set oLine = oSketch3D.SketchLines3D.AddByTwoPoints(oLine.EndPoint, oWPs(5), True, 1)

    <span style="color:blue">' Create a 2D sketch.</span>
    Dim oSketch As PlanarSketch
    Set oSketch = oCompDef.Sketches.Add(oCompDef.WorkPlanes.Item(2))

    <span style="color:blue">' Determine the model origin relative to the sketch space.</span>
    Dim oOrigin As Point2d
    Set oOrigin = oSketch.ModelToSketchSpace(oTG.CreatePoint(0, 0, 0))

    <span style="color:blue">' Create two lines.</span>
    Dim oNewPoint As Point2d
    Set oNewPoint = oTG.CreatePoint2d(oOrigin.X, oOrigin.Y - 4)
    Dim oSketchLine1 As SketchLine
    Set oSketchLine1 = oSketch.SketchLines.AddByTwoPoints(oOrigin, oNewPoint)
    oNewPoint.X = oNewPoint.X + 3
    Dim oSketchLine2 As SketchLine
    Set oSketchLine2 = oSketch.SketchLines.AddByTwoPoints(oSketchLine1.EndSketchPoint, oNewPoint)

    <span style="color:blue">' Create a fillet between the two lines.</span>
    Call oSketch.SketchArcs.AddByFillet(oSketchLine1, oSketchLine2, 1, oSketchLine1.StartSketchPoint.Geometry, oSketchLine2.EndSketchPoint.Geometry)

    <span style="color:blue">' Get the end of the 2d sketch in model space.</span>
    Dim oModelPoint As Point
    Set oModelPoint = oSketch.SketchToModelSpace(oSketchLine2.EndSketchPoint.Geometry)

    <span style="color:blue">' Create a work plane at the end of the 2D sketch.</span>
    Dim oWP As WorkPlane
    Set oWP = oCompDef.WorkPlanes.AddByNormalToCurve(oSketchLine2, oSketchLine2.EndSketchPoint)

    <span style="color:blue">' Create a path. Because the 3D and 2D sketches physically connect the path will include</span>
    <span style="color:blue">' both of them.</span>
    Dim oPath As Path
    Set oPath = oCompDef.Features.CreatePath(oSketchLine2)

    <span style="color:blue">' Create a sketch containing a circle.</span>
    Set oSketch = oCompDef.Sketches.Add(oWP)
    Set oOrigin = oSketch.ModelToSketchSpace(oTG.CreatePoint(0, 0, 0))
    Call oSketch.SketchCircles.AddByCenterRadius(oSketch.ModelToSketchSpace(oModelPoint), 0.375)

    <span style="color:blue">' Create a profile.</span>
    Dim oProfile As Profile
    Set oProfile = oSketch.Profiles.AddForSolid

    <span style="color:blue">' Create the sweep feature.</span>
    Dim oSweep As SweepFeature
    Set oSweep = oCompDef.Features.SweepFeatures.AddUsingPath(oProfile, oPath, kJoinOperation)
End Sub</pre>
      </div>
      <div id="iLogic" class="api-code">To run the sample have a part document open.<table width="100%" style="font-size:15">
          <tr>
            <td align="right"><button onclick="copyCode('iLogic_code')" class="copy-code">Copy Code</button></td>
          </tr>
        </table>
        <pre class="api-code" id="iLogic_code">    <span style="color:blue">' Set a reference to the currently active document.</span>
    <span style="color:blue">' This assumes that it is a part document.</span>
    Dim oPartDoc As PartDocument
    oPartDoc = ThisApplication.ActiveDocument

    <span style="color:blue">' Set a reference to the component definition.</span>
    Dim oCompDef As PartComponentDefinition
    oCompDef = oPartDoc.ComponentDefinition

    <span style="color:blue">' Set a reference to the transient geometry object.</span>
    Dim oTG As TransientGeometry
    oTG = ThisApplication.TransientGeometry

    <span style="color:blue">' Create some workpoints that will be used for the 3D sketch.</span>
    Dim oWPs(0 To 4) As WorkPoint
    oWPs(0) = oCompDef.WorkPoints.AddFixed(oTG.CreatePoint(0, 0, 0))
    oWPs(1) = oCompDef.WorkPoints.AddFixed(oTG.CreatePoint(3, 0, 0))
    oWPs(2) = oCompDef.WorkPoints.AddFixed(oTG.CreatePoint(3, 4, 0))
    oWPs(3) = oCompDef.WorkPoints.AddFixed(oTG.CreatePoint(3, 4, 2))
    oWPs(4) = oCompDef.WorkPoints.AddFixed(oTG.CreatePoint(6, 4, 2))

    <span style="color:blue">' Create a new 3D Sketch.</span>
    Dim oSketch3D As Sketch3D
    oSketch3D = oPartDoc.ComponentDefinition.Sketches3D.Add

    <span style="color:blue">' Draw 3D lines. The first line is drawn between two of the work points.</span>
    Dim oLine As SketchLine3D
    oLine = oSketch3D.SketchLines3D.AddByTwoPoints(oWPs(0), oWPs(1), True, 1)

    <span style="color:blue">' This second and subsequent lines are drawn between a 3D sketch point and a work point.</span>
    <span style="color:blue">' The work point is obtained from the previous line. Because the two lines will share</span>
    <span style="color:blue">' this 3D sketch point, Inventor will treat them as connected lines when creating any</span>
    <span style="color:blue">' paths.</span>
    oLine = oSketch3D.SketchLines3D.AddByTwoPoints(oLine.EndPoint, oWPs(2), True, 1)
    oLine = oSketch3D.SketchLines3D.AddByTwoPoints(oLine.EndPoint, oWPs(3), True, 0.75)
    oLine = oSketch3D.SketchLines3D.AddByTwoPoints(oLine.EndPoint, oWPs(4), True, 1)

    <span style="color:blue">' Create a 2D sketch.</span>
    Dim oSketch As PlanarSketch
    oSketch = oCompDef.Sketches.Add(oCompDef.WorkPlanes.Item(2))

    <span style="color:blue">' Determine the model origin relative to the sketch space.</span>
    Dim oOrigin As Point2d
    oOrigin = oSketch.ModelToSketchSpace(oTG.CreatePoint(0, 0, 0))

    <span style="color:blue">' Create two lines.</span>
    Dim oNewPoint As Point2d
    oNewPoint = oTG.CreatePoint2d(oOrigin.X, oOrigin.Y - 4)
    Dim oSketchLine1 As SketchLine
    oSketchLine1 = oSketch.SketchLines.AddByTwoPoints(oOrigin, oNewPoint)
    oNewPoint.X = oNewPoint.X + 3
    Dim oSketchLine2 As SketchLine
    oSketchLine2 = oSketch.SketchLines.AddByTwoPoints(oSketchLine1.EndSketchPoint, oNewPoint)

    <span style="color:blue">' Create a fillet between the two lines.</span>
    Call oSketch.SketchArcs.AddByFillet(oSketchLine1, oSketchLine2, 1, oSketchLine1.StartSketchPoint.Geometry, oSketchLine2.EndSketchPoint.Geometry)

    <span style="color:blue">' Get the end of the 2d sketch in model space.</span>
    Dim oModelPoint As Point
    oModelPoint = oSketch.SketchToModelSpace(oSketchLine2.EndSketchPoint.Geometry)

    <span style="color:blue">' Create a work plane at the end of the 2D sketch.</span>
    Dim oWP As WorkPlane
    oWP = oCompDef.WorkPlanes.AddByNormalToCurve(oSketchLine2, oSketchLine2.EndSketchPoint)

    <span style="color:blue">' Create a path. Because the 3D and 2D sketches physically connect the path will include</span>
    <span style="color:blue">' both of them.</span>
    Dim oPath As Path
    oPath = oCompDef.Features.CreatePath(oSketchLine2)

    <span style="color:blue">' Create a sketch containing a circle.</span>
    oSketch = oCompDef.Sketches.Add(oWP)
    oOrigin = oSketch.ModelToSketchSpace(oTG.CreatePoint(0, 0, 0))
    Call oSketch.SketchCircles.AddByCenterRadius(oSketch.ModelToSketchSpace(oModelPoint), 0.375)

    <span style="color:blue">' Create a profile.</span>
    Dim oProfile As Profile
    oProfile = oSketch.Profiles.AddForSolid

    <span style="color:blue">' Create the sweep feature.</span>
    Dim oSweep As SweepFeature
    oSweep = oCompDef.Features.SweepFeatures.AddUsingPath(oProfile, oPath, kJoinOperation)</pre>
      </div>
    </div><br><br><div id="CopyrightNotice" style="display:none;">
      <hr>
      <table width="100%" style="font-size:15">
        <tr>
          <td align="left">
                Â© Copyright 2025 Autodesk, Inc.
              </td>
          <td align="right"><a href="mailto:mfg.api.help.comments@autodesk.com?subject=Inventor%202026%20API%20Help%20(SweepFeature%20Sample%20Help%20Topic)">Comment on this page.</a></td>
        </tr>
      </table>
    </div>
  </body>
</html>