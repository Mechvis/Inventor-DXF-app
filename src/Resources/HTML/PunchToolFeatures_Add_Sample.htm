<html>
  <head>
    <META http-equiv="Content-Type" content="text/html; charset=utf-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge">
    <meta name="topic-type" content="reference-adsk">
    <meta name="helpsystempath" content="/view/INVNTOR/2026/ENU/">
    <link rel="canonical" href="/cloudhelp/2026/ENU/Inventor-API/files/PunchToolFeatures_Add_Sample.htm#!">
    <meta name="product" content="INVNTOR">
    <meta name="release" content="2026">
    <meta name="book" content="Developer">
    <meta name="component" content="Inventor-API">
    <meta name="created" content="">
    <meta name="user-comments" content="enable">
    <meta name="contextid" content="PunchToolFeatures_Add_Sample">
    <meta name="description" content="Add a punch tool feature Sample">
    <meta name="topicid" content="PunchToolFeatures_Add_Sample">
    <meta name="experiencelevel" content="expert">
    <meta name="audience" content="programmer">
    <meta name="language" content="ENU">
    <title>Add a punch tool feature</title>
    <link rel="stylesheet" href="../Style/jquery-ui.css"><script src="../Scripts/jquery-1.11.1.js"></script><script src="../Scripts/jquery-ui.js"></script><link rel="stylesheet" type="text/css" href="../Style/APIHelp-chm.css">
    <link rel="stylesheet" type="text/css" href="/view/clientframework/client.css"><script type="text/javascript" src="/view/clientframework/client.js"></script><script>
          // This turns on the display of the copyright notice which is not visible by default.
          // Because the head is removed when used in the web help, this function will only exists
          // when the chm help is being created.
          window.onload = function() {
          copyrightElement = document.getElementById('CopyrightNotice');
          copyrightElement.style.display = 'block';
          };
        </script><script>
          $(function() {
          $( "#tabs" ).tabs();
          });
        </script></head>
  <body><script>
          function copyCode(language) {
          // Get the pre element for the specified language.
          var preElement = document.getElementById(language);

          // Get the text from the element and copy it to the clipboard.
          var preText = preElement.innerText;

          var isIE = /*@cc_on!@*/false || !!document.documentMode;

          if (isIE)
          {
            window.clipboardData.setData('Text', preText);
          }
          else
          {
            navigator.clipboard.writeText(preText);
          }

          //alert("Code copied to clipboard");
          }
        </script><link rel="stylesheet" type="text/css" href="../Style/APIHelp.css">
    <h1 class="api">Add a punch tool feature</h1>
    <h2 class="api">
          Description
        </h2>This program demonstrates the creation of a punch tool feature. It uses one of the punch features that's delivered with Inventor. It assumes you already have an existing sheet metal part and have selected a face to place the punch feature on. The selected face should be large so there is room for the punch features.<h2 class="api">
          Code Samples
        </h2>
    <div id="tabs">
      <ul>
        <li><a href="#VBA">VBA</a></li>
        <li><a href="#iLogic">iLogic</a></li>
      </ul>
      <div id="VBA" class="api-code">
        <table width="100%" style="font-size:15">
          <tr>
            <td align="right"><button onclick="copyCode('VBA_code')" class="copy-code">Copy Code</button></td>
          </tr>
        </table>
        <pre class="api-code" id="VBA_code">Public Sub PlacePunchFeature()
    <span style="color:blue">' Get the active sheet metal document and component</span>
    <span style="color:blue">' definition of the active document.</span>
    On Error Resume Next
    Dim oPartDoc As PartDocument
    Set oPartDoc = ThisApplication.ActiveDocument
    If Err Then
        MsgBox "A part must be active."
        Exit Sub
    End If
    Dim oSMDef As SheetMetalComponentDefinition
    Set oSMDef = oPartDoc.ComponentDefinition

    <span style="color:blue">' Get the selected face that will be used for the creation</span>
    <span style="color:blue">' of the sketch that will contain the sketch points.</span>
    Dim oFace As Face
    Set oFace = oPartDoc.SelectSet.Item(1)
    If Err Then
        MsgBox "A planar face must be selected."
        Exit Sub
    End If
    On Error GoTo 0

    If oFace.SurfaceType  kPlaneSurface Then
        MsgBox "A planar face must be selected."
        Exit Sub
    End If

    <span style="color:blue">' Create a sketch on the selected face.</span>
    Dim oSketch As PlanarSketch
    Set oSketch = oSMDef.Sketches.Add(oFace)

    <span style="color:blue">' Create some points on the sketch.  The model will need to</span>
    <span style="color:blue">' be of a size that these points lie on the model.</span>
    Dim oPoints As ObjectCollection
    Set oPoints = ThisApplication.TransientObjects.CreateObjectCollection

    Dim oTG As TransientGeometry
    Set oTG = ThisApplication.TransientGeometry

    Dim oPoint As SketchPoint
    Set oPoint = oSketch.SketchPoints.Add(oTG.CreatePoint2d(2, 2), True)
    Call oPoints.Add(oPoint)

    Set oPoint = oSketch.SketchPoints.Add(oTG.CreatePoint2d(8, 3), True)
    Call oPoints.Add(oPoint)

    Set oPoint = oSketch.SketchPoints.Add(oTG.CreatePoint2d(2, 10), False)
    Call oPoints.Add(oPoint)
    Dim oSMFeatures As SheetMetalFeatures
    Set oSMFeatures = oSMDef.Features

    <span style="color:blue">' Create an iFeatureDefinition object for a punch tool.</span>
    Dim oiFeatureDef As iFeatureDefinition
    Set oiFeatureDef = oSMFeatures.PunchToolFeatures.CreateiFeatureDefinition( _
        "C:\Users\Public\Documents\Autodesk\Inventor 2026\Catalog\Punches\keyhole.ide")

    <span style="color:blue">' Set the input.</span>
    Dim oInput As iFeatureInput
    For Each oInput In oiFeatureDef.iFeatureInputs
        Dim oParamInput As iFeatureParameterInput
        Select Case oInput.Name
            Case "Length"
                Set oParamInput = oInput
                oParamInput.Expression = "1 in"
            Case "Hole_Diameter"
                Set oParamInput = oInput
                oParamInput.Expression = "0.5 in"
            Case "Slot_Width"
                Set oParamInput = oInput
                oParamInput.Expression = "0.3875 in"
            Case "Fillet"
                Set oParamInput = oInput
                oParamInput.Expression = "0.0625 in"
            Case "Thickness"
                Set oParamInput = oInput
                oParamInput.Expression = "0.125 in"
        End Select
    Next

    <span style="color:blue">' Create the iFeature at a 45 degree angle.</span>
    Dim oPunchTool As PunchToolFeature
    Set oPunchTool = oSMFeatures.PunchToolFeatures.Add(oPoints, oiFeatureDef, 3.14159265358979 / 4)
End Sub</pre>
      </div>
      <div id="iLogic" class="api-code">
        <table width="100%" style="font-size:15">
          <tr>
            <td align="right"><button onclick="copyCode('iLogic_code')" class="copy-code">Copy Code</button></td>
          </tr>
        </table>
        <pre class="api-code" id="iLogic_code">    <span style="color:blue">' Get the active sheet metal document and component</span>
    <span style="color:blue">' definition of the active document.</span>
    On Error Resume Next
    Dim oPartDoc As PartDocument
    oPartDoc = ThisApplication.ActiveDocument
    If Err.Number &gt; 0 Then
        MsgBox("A part must be active.")
        Exit Sub
    End If
    Dim oSMDef As SheetMetalComponentDefinition
    oSMDef = oPartDoc.ComponentDefinition

    <span style="color:blue">' Get the selected face that will be used for the creation</span>
    <span style="color:blue">' of the sketch that will contain the sketch points.</span>
    Dim oFace As Face
    oFace = oPartDoc.SelectSet.Item(1)
    If Err.Number &gt; 0 Then
        MsgBox("A planar face must be selected.")
        Exit Sub
    End If
    On Error GoTo 0

    If oFace.SurfaceType &lt;&gt; kPlaneSurface Then
        MsgBox("A planar face must be selected.")
        Exit Sub
    End If

    <span style="color:blue">' Create a sketch on the selected face.</span>
    Dim oSketch As PlanarSketch
    oSketch = oSMDef.Sketches.Add(oFace)

    <span style="color:blue">' Create some points on the sketch.  The model will need to</span>
    <span style="color:blue">' be of a size that these points lie on the model.</span>
    Dim oPoints As ObjectCollection
    oPoints = ThisApplication.TransientObjects.CreateObjectCollection

    Dim oTG As TransientGeometry
    oTG = ThisApplication.TransientGeometry

    Dim oPoint As SketchPoint
    oPoint = oSketch.SketchPoints.Add(oTG.CreatePoint2d(2, 2), True)
    Call oPoints.Add(oPoint)

    oPoint = oSketch.SketchPoints.Add(oTG.CreatePoint2d(8, 3), True)
    Call oPoints.Add(oPoint)

    oPoint = oSketch.SketchPoints.Add(oTG.CreatePoint2d(2, 10), False)
    Call oPoints.Add(oPoint)
    Dim oSMFeatures As SheetMetalFeatures
    oSMFeatures = oSMDef.Features

    <span style="color:blue">' Create an iFeatureDefinition object for a punch tool.</span>
    Dim oiFeatureDef As iFeatureDefinition
    oiFeatureDef = oSMFeatures.PunchToolFeatures.CreateiFeatureDefinition( _
        "C:\Users\Public\Documents\Autodesk\Inventor 2026\Catalog\Punches\keyhole.ide")

    <span style="color:blue">' Set the input.</span>
    Dim oInput As iFeatureInput
    For Each oInput In oiFeatureDef.iFeatureInputs
        Dim oParamInput As iFeatureParameterInput
        Select Case oInput.Name
            Case "Length"
                oParamInput = oInput
                oParamInput.Expression = "1 in"
            Case "Hole_Diameter"
                oParamInput = oInput
                oParamInput.Expression = "0.5 in"
            Case "Slot_Width"
                oParamInput = oInput
                oParamInput.Expression = "0.3875 in"
            Case "Fillet"
                oParamInput = oInput
                oParamInput.Expression = "0.0625 in"
            Case "Thickness"
                oParamInput = oInput
                oParamInput.Expression = "0.125 in"
        End Select
    Next

    <span style="color:blue">' Create the iFeature at a 45 degree angle.</span>
    Dim oPunchTool As PunchToolFeature
    oPunchTool = oSMFeatures.PunchToolFeatures.Add(oPoints, oiFeatureDef, 3.14159265358979 / 4)</pre>
      </div>
    </div><br><br><div id="CopyrightNotice" style="display:none;">
      <hr>
      <table width="100%" style="font-size:15">
        <tr>
          <td align="left">
                Â© Copyright 2025 Autodesk, Inc.
              </td>
          <td align="right"><a href="mailto:mfg.api.help.comments@autodesk.com?subject=Inventor%202026%20API%20Help%20(PunchToolFeatures_Add%20Sample%20Help%20Topic)">Comment on this page.</a></td>
        </tr>
      </table>
    </div>
  </body>
</html>